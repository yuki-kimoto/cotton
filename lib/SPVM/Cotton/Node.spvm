class Cotton::Node {
  allow Cotton;
  allow Cotton::App;
  allow Cotton::Renderer;
  
  use Hash;
  use StringList;
  use Cotton::Style::Color;
  use StringBuffer;
  use Cotton::Style;
  use Cotton::Attributes;
  
  enum {
    TYPE_ELEMENT_NODE = 1,
    TYPE_ATTRIBUTE_NODE = 2,
    TYPE_TEXT_NODE = 3,
    TYPE_CDATA_SECTION_NODE = 4,
    TYPE_PROCESSING_INSTRUCTION_NODE = 7,
    TYPE_COMMENT_NODE = 8,
    TYPE_DOCUMENT_NODE = 9,
    TYPE_DOCUMENT_TYPE_NODE = 10,
    TYPE_DOCUMENT_FRAGMENT_NODE = 11,
  }
  
  has type : ro int;
  
  has tag_name : rw string;
  
  has text : ro StringBuffer;
  
  has nodes : ro List of Cotton::Node;
  
  has attributes : Cotton::Attributes;
  
  has parent : Cotton::Node;
  
  has color : rw Cotton::Style::Color;
  
  has background_color : rw Cotton::Style::Color;
  
  has draw_left : rw int;
  
  has draw_top : rw int;
  
  has draw_width : rw int;
  
  has draw_height : rw int;
  
  static method new : Cotton::Node ($type : int, $nodes : Cotton::Node[] = undef) {
    
    my $self = new Cotton::Node;
    
    $self->{type} = $type;
    
    $self->{nodes} = List->new($nodes);
    
    if ($nodes) {
      for my $node (@$nodes) {
        $node->{parent} = $self;
        weaken $node->{parent};
      }
    }
    
    return $self;
  }
  
  static method new_element : Cotton::Node ($tag_name : string, $nodes : Cotton::Node[] = undef, $attribute_pairs : object[] = undef) {
    
    unless ($tag_name) {
      die "\$tag_namet must be defined.";
    }
    
    my $self = Cotton::Node->new(Cotton::Node->TYPE_ELEMENT_NODE, $nodes);
    
    $self->{tag_name} = $tag_name;
    
    my $attributes = Cotton::Attributes->new($attribute_pairs);
    
    $self->{attributes} = $attributes;
    
    my $style_pairs = (object[])$attributes->attributes_h->delete("style");
    
    my $style = Cotton::Style->new($style_pairs);
    
    $attributes->{style} = $style;
    
    return $self;
  }
  
  static method new_text : Cotton::Node ($text : string) {
    
    unless ($text) {
      die "\$text must be defined.";
    }
    
    my $self = Cotton::Node->new(Cotton::Node->TYPE_TEXT_NODE);
    
    my $text_buffer = StringBuffer->new;
    
    $text_buffer->push($text);
    
    $self->{text} = $text_buffer;
    
    return $self;
  }
  
  # Instance Methods
  method style : Cotton::Style () {
    return $self->{attributes}->{style};
  }
  
  method set_style : void ($style : Cotton::Style) {
    $self->{attributes}->{style} = $style;
  }
}

